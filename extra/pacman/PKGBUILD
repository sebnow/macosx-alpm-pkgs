# Contributor: Sebastian Nowicki <sebnow@gmail.com>
# Original contributers:
# Contributer: Dan McGee <dan@archlinux.org>
# Contributer: Aaron Griffin <aaron@archlinux.org>

pkgname=pacman
pkgver=3.2.2
pkgrel=1
pkgdesc="A library-based package manager with dependency support"
arch=('i386' 'x86_64')
url="http://www.archlinux.org/pacman/"
license=('GPL')
depends=('bash' 'libarchive>=2.5.5' 'libfetch')
optdepends=('python: for rankmirrors script usage')
backup=('etc/pacman.conf' 'etc/makepkg.conf')
options=('!libtool')
source=("ftp://ftp.archlinux.org/other/$pkgname/$pkgname-$pkgver.tar.gz")
md5sums=('8404fc38566bb52f8eee2ea483a41051')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  
  ./configure --prefix=/usr/local --sysconfdir=/etc --localstatedir=/var
  make || return 1
  make DESTDIR="$pkgdir" install || return 1

  # install completion files
  mkdir -p "$pkgdir/etc/bash_completion.d/"
  install -m644 contrib/bash_completion "$pkgdir/etc/bash_completion.d/pacman"
  mkdir -p "$pkgdir/usr/share/zsh/site-functions/"
  install -m644 contrib/zsh_completion "$pkgdir/usr/share/zsh/site-functions/_pacman"
}

# vim: set ts=2 sw=2 et:
